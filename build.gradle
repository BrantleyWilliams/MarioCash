buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'

group = 'dev.zhihexireng'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

ext {
    grpcVersion = '1.11.0'
    leveldbVersion = '0.10'
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile('org.springframework.boot:spring-boot-starter')
        compile "org.iq80.leveldb:leveldb:${leveldbVersion}"

        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
        testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    }
}

project(':mariocash-core') {
    apply plugin: 'com.google.protobuf'

    dependencies {
        compile "io.grpc:grpc-protobuf:${grpcVersion}"
        compile "io.grpc:grpc-stub:${grpcVersion}"
        compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
        compile('org.lognet:grpc-spring-boot-starter:2.3.1')

        testCompile "io.grpc:grpc-testing:${grpcVersion}"
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.5.1-1"
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.11.0'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'build/generated/source/proto/main/grpc'
                srcDirs 'build/generated/source/proto/main/java'
            }
        }
    }
}
